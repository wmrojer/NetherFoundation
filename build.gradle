buildscript {
    repositories {
        jcenter()
        maven { url = 'http://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = '1.12.2-1.1.3'
group = 'tehseph.netherfoundation'
archivesBaseName = 'NetherFoundation'

compileJava { sourceCompatibility = targetCompatibility = '1.8' }

minecraft {

    version = '1.12.2-14.23.4.2705'
    mappings = 'snapshot_20180525' // 'snapshot_20171003'
    runDir = 'run'
    makeObfSourceJar = false

}

repositories {

    maven { url = 'http://maven.covers1624.net' } // Team CoFH
    maven { url = 'http://chickenbones.net/maven/' } // Chickenbones
    maven { url = 'http://dvs1.progwml6.com/files/maven' } // JEI / Tinkers Construct
    maven { url = 'http://tehnut.info/maven' } // HWYLA / WAILA

}

dependencies {

    def noJEI = { exclude module: 'jei_1.12.2' }

    deobfCompile 'cofh:ThermalFoundation:1.12.2-2.5.+:deobf', noJEI

    deobfProvided 'cofh:ThermalExpansion:1.12.2-5.5.+:deobf'

    deobfProvided 'slimeknights.mantle:Mantle:1.12-1.3.+'
    deobfProvided 'slimeknights:TConstruct:1.12.2-2.10.+'

    deobfProvided 'mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2'

}

processResources {

    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

}

jar { classifier = 'universal' }

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}
